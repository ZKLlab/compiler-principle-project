<程序> ::= <分程序> <Period>
<分程序> ::= <分程序1> <分程序2> <分程序3> <语句>
<分程序1> ::= <Nul> | <常量说明>
<分程序2> ::= <Nul> | <变量说明>
<分程序3> ::= <Nul> | <过程说明>
<常量说明> ::= <ConstSym> <常量定义> <常量说明1> <Semicolon>
<常量说明1> ::= <Nul> | <Comma> <常量定义> <常量说明1>
<常量定义> ::= <标识符> <Eql> <Number>
<变量说明> ::= <VarSym> <标识符> <变量说明1> <Semicolon>
<变量说明1> ::= <Nul> | <Comma> <标识符> <变量说明1>
<标识符> ::= <Ident> <标识符1>
<标识符1> ::= <Nul> | <标识符2> <标识符1>
<标识符2> ::= <Ident> | <Number>
<过程说明> ::= <过程说明1> <过程说明2>
<过程说明1> ::= <过程首部> <分程序> <Semicolon>
<过程说明2> ::= <Nul> | <过程说明1> <过程说明2>
<过程首部> ::= <ProcedureSym> <标识符> <Semicolon>
<语句> ::= <赋值语句> | <条件语句> | <当循环语句> | <过程调用语句> | <复合语句> | <读语句> | <写语句> | <Nul>
<赋值语句> ::= <标识符> <Becomes> <表达式>
<复合语句> ::= <BeginSym> <语句> <复合语句1> <EndSym>
<复合语句1> ::= <Nul> | <Semicolon> <语句> <复合语句1>
<条件表达式> ::= <表达式> <关系运算符> <表达式> | <OddSym> <表达式>
<表达式> ::= <表达式1> <项> <表达式2>
<表达式1> ::= <Nul> | <Plus> | <Minus>
<表达式2> ::= <Nul> | <加减法运算符> <项> <表达式2>
<项> ::= <因子> <项1>
<项1> ::= <Nul> | <乘除法运算符> <因子> <项1>
<因子> ::= <标识符> | <Number> | <LParen> <表达式> <RParen>
<加减法运算符> ::= <Plus> | <Minus>
<乘除法运算符> ::= <Times> | <Slash>
<关系运算符> ::= <Eql> | <Neq> | <Lss> | <Leq> | <Gtr> | <Geq>
<条件语句> ::= <IfSym> <条件表达式> <ThenSym> <语句>
<过程调用语句> ::= <CallSym> <标识符>
<当循环语句> ::= <WhileSym> <条件表达式> <DoSym> <语句>
<读语句> ::= <ReadSym> <LParen> <标识符> <读语句1> <RParen>
<读语句1> ::= <Nul> | <Comma> <标识符> <读语句1>
<写语句> ::= <WriteSym> <LParen> <表达式> <写语句1> <RParen>
<写语句1> ::= <Nul> | <Comma> <表达式> <写语句1>